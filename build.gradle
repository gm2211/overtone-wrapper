// Plugins
apply plugin: "java"
apply plugin: "idea"
apply from: "idea.gradle"
apply from: "utils.gradle"

repositories {
    jcenter()
    maven { url "https://clojars.org/repo" }
}

dependencies {
    compile "overtone:overtone:$overtone_version"
    compile "org.clojure:clojure:$clojure_version"
    compile "com.google.guava:guava:$guava_version"

    // Serialisation
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_yaml_version"


    // Tests
    testCompile "junit:junit:$junit_version"
}

project.ext {
    overtoneLibsDir = "/libs/overtone"
}

task explodeDeps(type: Copy) {
    def overtoneFolder = "${project.getProjectDir().getPath()}/src/main/resources$overtoneLibsDir"

    // Extracting native libraries for scsynth
    configurations.runtime.filter { it.name.contains ("scsynth") }.each {
        from zipTree(it).matching  {
            include "native/macosx/**"
        }
        into "$overtoneFolder"
    }
}

task configureWrapper {
    def native_lib_path = "nativeLibrariesPath: $project.overtoneLibsDir/native/macosx/x86_64"

    // Add native libraries path to wrapper configuration
    appendToFile("${project.getProjectDir().getPath()}/$wrapper_config_path", native_lib_path)
}

test {
    useJUnit {
        excludeCategories "overtone.wrapper.SlowTest"
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories "overtone.wrapper.SlowTest"
    }
}

configureWrapper.dependsOn explodeDeps
build.dependsOn configureWrapper
tasks.idea.dependsOn configureWrapper
